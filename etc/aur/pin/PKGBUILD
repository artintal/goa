# -*- shell-script -*-
# Maintainer: Jon Eyolfson <jon@eyolfson.com>
# Contributor: Roni Choudhury <aichoudh@gmail.com>
#
# Modified by: Eric Schulte <eschulte@cs.unm.edu>
pkgname=pin
vendor_pkgver=2.11-49306-gcc.3.4.6-ia32_intel64-linux
pkgver=2.11_49306
_pkgver=2.11-49306
pkgrel=1
pkgdesc="A tool for the dynamic instrumentation of programs"
gccver=3.4.6
arch=('x86_64')
url="http://www.pintool.org"
license=('custom')
groups=()
depends=('gcc>=3.4' 'bash' 'lib32-gcc-libs>=3.4')
options=('!strip')
source=("http://software.intel.com/sites/landingpage/pintool/downloads/${pkgname}-${vendor_pkgver}.tar.gz"
        "pin.sh")
sha1sums=('d4778eda84f6fa42fe76c29dcb363e4272363f08'
          'bb65958156bff20a8feaec0c7a1b9fa760d02cc6')

build() {
    msg "not building"
}

package() {
    cd "${srcdir}/${pkgname}-${vendor_pkgver}"

    # Copy pin executables, etc. to the /opt/pin directory (because pin
    # uses a non-standard FHS for its stuff).
    mkdir -p ${pkgdir}/opt/pin
    cp -a doc extras ia32 intel64 source ${pkgdir}/opt/pin
    find ${pkgdir}/opt/pin -type d -exec chmod 755 {} \+
    chmod 755 ${pkgdir}/opt/pin/{doc,extras,ia32,intel64,source}
    install -m 644 README ${pkgdir}/opt/pin
    install pin ${pkgdir}/opt/pin

    # Copy license documents to license directory.
    mkdir -p ${pkgdir}/usr/share/licenses/pin
    install -m 644 LICENSE ${pkgdir}/usr/share/licenses/pin

    # Copy shell script for modifying path.
    cd "$srcdir"
    mkdir -p ${pkgdir}/etc/profile.d 
    install pin.sh ${pkgdir}/etc/profile.d
}
