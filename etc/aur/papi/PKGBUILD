# -*- shell-script -*-
# Contributor: Jed Brown <jed@59A2.org>
#
# Modified by: Eric Schulte <schulte.eric@gmail.com>
#
pkgname=papi-dev
_pkgname=papi
pkgver=5.1.0.2
_pkgver=5.1.0
pkgrel=1
pkgdesc="Performance Application Programming Interface"
arch=(x86_64 i686)
url="http://icl.cs.utk.edu/papi/"
license=('BSD')
depends=(glibc linux-api-headers)
makedepends=()
provides=(papi)
conflicts=(papi)
replaces=(papi)
source=("http://icl.cs.utk.edu/projects/papi/downloads/${_pkgname}-${pkgver}.tar.gz"
    "papi.sh")
md5sums=('e1f2d412e7cc249ba487ed50cfcd1f4a'
         '2d187e36232eae10fbc71aa50fdd552d')
build() {
  cd ${srcdir}/${_pkgname}-${_pkgver}/src

  unset MAKEFLAGS # Makefiles are broken for parallel make
  ./configure --prefix=/usr --mandir=/usr/share/man --with-perf-events \
    LDFLAGS="${LDFLAGS} -Wl,--no-as-needed" # Configure has a problem detecting dlopen()
  make
}

package() {
  cd ${srcdir}/${_pkgname}-${_pkgver}/src
  make DESTDIR=${pkgdir} install
  cd ../
  cp -a src ${pkgdir}/usr/share/papi/
  chmod 644 ${pkgdir}/usr/share/papi/papi_events.csv
  install -D ${srcdir}/${_pkgname}-${_pkgver}/LICENSE.txt ${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE

  # Copy shell script for modifying path.
  cd "$srcdir"
  mkdir -p ${pkgdir}/etc/profile.d 
  install papi.sh ${pkgdir}/etc/profile.d
}
